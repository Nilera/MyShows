apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'realm-android'
apply plugin: 'com.getkeepsafe.dexcount'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.0'

    defaultConfig {
        applicationId 'me.myshows.android'
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName '1.0'

        multiDexEnabled true

        buildConfigField 'String', 'CLIENT_ID', "\"${System.env.CLIENT_ID}\""
        buildConfigField 'String', 'CLIENT_SECRET', "\"${System.env.CLIENT_SECRET}\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    testOptions {
        unitTests.all {
            testLogging {
                events 'passed', 'skipped', 'failed'
                exceptionFormat = 'full'
            }
            afterSuite { desc, result ->
                if (!desc.parent) {
                    def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                    println("\n$output\n")
                }
            }
        }
    }
}

dependencies {
    implementation libraries.kotlin

    implementation libraries.appCompat
    implementation libraries.cardView
    implementation libraries.design
    implementation libraries.preferenceV14
    implementation libraries.recyclerView
    implementation libraries.supportAnnotations
    implementation libraries.supportV13

    implementation (libraries.advRecyclerView) { transitive = true }
    implementation libraries.circleImageView
    implementation libraries.circleIndicator

    implementation libraries.jackson
    implementation libraries.jacksonKotlin

    implementation libraries.okhttp
    implementation libraries.okhttpLogging
    implementation libraries.okhttpUrlConnection
    implementation libraries.retrofit
    implementation libraries.retrofitRxJava
    implementation libraries.retrofitJackson
    kapt libraries.glideCompiler
    implementation libraries.glide
    implementation libraries.glideOkhttp

    implementation libraries.rxJava
    implementation libraries.rxAndroid
    implementation libraries.rxLifecycle
    implementation libraries.rxLifecycleComponents

    kapt libraries.daggerProcessor
    implementation libraries.dagger

    kapt libraries.parceler
    implementation libraries.parcelerApi

    implementation libraries.commonsCodec
    implementation libraries.commonsIO

    debugImplementation libraries.leakCanary
    releaseImplementation libraries.leakCanaryNoOp

    testImplementation libraries.junit
    testImplementation libraries.assertj
    testImplementation libraries.mockwebserver
}
